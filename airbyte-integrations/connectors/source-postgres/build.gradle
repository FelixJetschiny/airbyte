import org.jsonschema2pojo.SourceType

plugins {
    id 'airbyte-java-connector'
    id 'java-library'
    id "org.jsonschema2pojo" version "1.2.1"
    id 'maven-publish'
}

group = 'io.airbyte.connectors'
version = '1.0.0-SNAPSHOT'

java {
    compileJava {
        options.compilerArgs += "-Xlint:-try,-rawtypes,-unchecked"
    }
}

airbyteJavaConnector {
    cdkVersionRequired = '0.48.4'
    features = ['db-sources', 'datastore-postgres']
    useLocalCdk = false
}

application {
    mainClass = 'io.airbyte.integrations.source.postgres.PostgresSource'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

dependencies {
    implementation 'commons-codec:commons-codec:1.16.0'
    implementation 'io.debezium:debezium-embedded:3.0.1.Final'
    implementation 'io.debezium:debezium-connector-postgres:3.0.1.Final'

    testFixturesApi 'org.testcontainers:postgresql:1.19.0'

    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

jsonSchema2Pojo {
    sourceType = SourceType.YAMLSCHEMA
    source = files("${sourceSets.main.output.resourcesDir}/internal_models")
    targetDirectory = new File(project.buildDir, 'generated/src/gen/java/')
    removeOldOutput = true

    targetPackage = 'io.airbyte.integrations.source.postgres.internal.models'

    useLongIntegers = true
    generateBuilders = true
    includeConstructors = false
    includeSetters = true
}

compileKotlin {
    dependsOn {
        generateJsonSchema2Pojo
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}

tasks.named("sourcesJar") {
    dependsOn("generateJsonSchema2Pojo")
}
